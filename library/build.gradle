apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 1

group = 'co.kyash'
version = "$versionMajor.$versionMinor.$versionPatch"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "$versionMajor.$versionMinor.$versionPatch"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    //==================== Kotlin ====================
    implementation depends.kotlin.stdlib

    //==================== Support Library ====================
    implementation depends.support.appcompat

    //==================== Test ====================
    testImplementation depends.junit
    testImplementation depends.mockitoKotlin
    testImplementation depends.robolectric.core
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}